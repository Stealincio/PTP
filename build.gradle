import static groovy.io.FileType.*

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'checkstyle'
apply plugin: 'jacoco'
apply plugin: 'findbugs'

sourceCompatibility = 1.7

sourceSets {
	main {
		java {
			srcDir 'src/main/java'
		}
	}
	test {
		java {
			srcDir 'src/test/java'
		}
	}
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination "${buildDir}/jacocoHtml"
    }

	afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: 'edu/kit/tm/ptp/examples/**')
        })
    }
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

repositories {
	jcenter()
}

dependencies {
	compile group: 'net.freehaven.tor.control', name: 'jtorctl', version: '0.2'
	compile group: 'commons-net', name: 'commons-net', version: '3.3'
	compile group: 'commons-codec', name: 'commons-codec', version: '1.10'
	compile group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.54'
	compile group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.54'
	compile group: 'com.esotericsoftware', name: 'kryo', version: '3.0.3'
	compile group: 'com.google.code.findbugs', name: 'findbugs-annotations', version: '3.0.1'
	testCompile group: 'junit', name: 'junit', version: '4.+'
}

checkstyle {
	toolVersion = "6.12.1"
}

// Create tasks to create a jar file for each example
File srcDir = file(sourceSets.main.java.srcDirs[0].getAbsolutePath() + '/edu/kit/tm/ptp/examples')
def names = []
srcDir.eachFileMatch FILES, ~/.*\.java/, { file -> names << file.name  }
String exName

names.each { fileName -> 
   	exName = fileName.split("\\.")[0]
	tasks.create(name: exName, type:Jar) {
		from sourceSets.main.output
		manifest {
			attributes 'Main-Class': 'edu.kit.tm.ptp.examples.' + exName
		}
		from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
		exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
		baseName = exName
		destinationDir = new File("build/examples")
	}
}

jar.dependsOn {
	tasks.findAll { task -> task.name.contains('Example') }
}
