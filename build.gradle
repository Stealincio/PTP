apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'checkstyle'
apply plugin: 'jacoco'

sourceSets {
	main {
		java {
			srcDir 'src/main/java'
		}
	}
	test {
		java {
			srcDir 'src/test/java'
		}
	}
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination "${buildDir}/jacocoHtml"
    }
}

repositories {
	jcenter()
}

dependencies {
	compile group: 'net.freehaven.tor.control', name: 'jtorctl', version: '0.2'
	compile group: 'commons-net', name: 'commons-net', version: '3.3'
	compile group: 'com.esotericsoftware', name: 'kryo', version: '3.0.3'
	testCompile group: 'junit', name: 'junit', version: '4.+'
}

checkstyle {
	toolVersion = "6.12.1"
}

// Create tasks to create a jar file for each example
File srcDir = file(sourceSets.main.java.srcDirs[0].getAbsolutePath() + '/edu/kit/tm/ptp/examples')
FileCollection collection = files { srcDir.listFiles() }
String exName

collection.each {File file ->
   	exName = file.getName().split("\\.")[0]
	tasks.create(name: exName, type:Jar) {
		from sourceSets.main.output
		manifest {
			attributes 'Main-Class': 'edu.kit.tm.ptp.examples.' + exName
		}
		from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
		baseName = exName
	}
}

jar.dependsOn {
	tasks.findAll { task -> task.name.contains('Example') }
}
